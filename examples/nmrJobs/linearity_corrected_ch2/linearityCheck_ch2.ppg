// 
version=2.0;

uses = defaultGates.gate;
// defaultGates.gate is located in the "gates" folder in the ~/.opencorenmr directory

transform F2Freq=#+180;
// DDS(II) signal is to be down-converted with an intermediate frequency of 180 MHz.
// Accordingly, we want it generating the frequency 180 MHz above the carrier freq.


Transform F2Amp=0.831747
	+ 0.798307 * #
	- 0.00397388 * pow(#,2)
	+ 8.4035e-05 * pow(#,3)
	- 4.53503e-07 * pow(#,4)
	+ 1.94319e-09 * pow(#,5);

//Final set of parameters            Asymptotic Standard Error
//=======================            ==========================
//a0              = 0.831747         +/- 0.06474      (7.783%)
//a1              = 0.798307         +/- 0.01394      (1.746%)
//a2              = -0.00397338      +/- 0.0008856    (22.29%)
//a3              = 8.4035e-05       +/- 2.248e-05    (26.75%)
//a4              = -4.53503e-07     +/- 2.459e-07    (54.22%)
//a5              = 1.94319e-09      +/- 9.68e-10     (49.82%)

CLK=160;  // PPG Clock frequency 

freq f2=40.2; 

amp a=100; 


const int AD9858_2GHZ_DISABLE=16472; 
// This is not going to be altered, and thus is declared with "const",
// so that it does not appear in the variable edit table.

//-----  aux parameters  -----
aux PD=0.2s(Pulse Delay); 
aux NA=20(Number of Accum); 
aux DW=5u(DWell time); 
aux AL=100(Acquisition Length); 
aux ND=0(Number of Dummy scans); 

//----- phase cycle  -----
phaselist pList=(ch2; x,-x);
acqphase=x,-x;

// Any command before "start" should finish with a semicolon (;).
start   
// Implementation begins with a "start" command.
// Commands between "start" and "init" is implemented ONCE for each run.

  pulse(50n; F2FreqRST)
//  AD9858 (DDS chip) initialization
  pulse(50n; F2Freq(setup;AD9858_2GHz_DISABLE))
//  We use a 1 GHz clock, and disable 2 GHz divider, 

  delay(1m)
  pulse(5000n; F2Freq(f2))
  delay(1m)

Init // Commeands between "Init" and "Relax" are repeated NA(+ND) times.  
  pulse(10u;                           F2_Unblank, RG, F1TTL1)
  pulse(50n;                           F2_Unblank, RG, ST)
  pulse[al](dw*al; F2Amp(a*#/al), pList, F2_Gate, F2_Unblank,RG)
  delay(10m)
relax   
